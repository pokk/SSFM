apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'
apply plugin: 'org.fabiomsr.drawableoptimizer'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def buildDateTime = project.hasProperty('devBuild') ? 100 : new Date().format('yyMMddHHmm').toInteger()

// From Kotlin 1.1.4, we don't have to use findByView in viewHolder or customize view.
androidExtensions { experimental = true }
kapt { generateStubs = true }

android {
    compileSdkVersion 25
    buildToolsVersion "26.0.1"
    defaultConfig {
        applicationId "taiwan.no1.app"
        minSdkVersion 21
        targetSdkVersion 25
        multiDexEnabled true
        versionCode buildDateTime
        versionName "1.0"
        renderscriptTargetApi 19
        renderscriptSupportModeEnabled true    // Enable RS support
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField "long", "BUILD_TIMESTAMP", project.hasProperty("devBuild") ? "000000000" : System.currentTimeMillis() + "L"
    }
    buildTypes {
        release { minifyEnabled false }
        debug {
            signingConfig signingConfigs.debug
            ext.alwaysUpdateBuildId = false
            resValue("string", "PORT_NUMBER", "8081")
        }
    }
    sourceSets {
        main {
            java.srcDirs += 'src/main/kotlin'
            assets.srcDirs += 'assets'
        }
    }
    packagingOptions { exclude 'META-INF/rxjava.properties' }
    configurations.all { resolutionStrategy.force "com.google.code.findbugs:jsr305:3.0.1" }
    dataBinding { enabled = true }
    dexOptions { javaMaxHeapSize "4g" }
    drawableOptimizer {
        optimizer = 'zopfli'
        compressionLevel = 9
        iterations = 15
        onlyOnRelease = true
        logLevel = 'info'
    }
    if (project.hasProperty('devBuild')) {
        aaptOptions.cruncherEnabled = false
        splits.abi.enable = false
        splits.density.enable = false
    }
    flavorDimensions "default"
    productFlavors {
        development {
            minSdkVersion 21
            dimension "default"
            resConfigs("en", "xxhdpi")
        }
        production { dimension "default" }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "com.android.support:multidex:1.0.2"
    // My own tool library
    implementation "com.devrapid.jieyi:kotlinknifer:1.0.12"
    implementation "com.devrapid.jieyi:musicdiskplayer:0.0.2"
    /* For language */
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
//    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    // Kotlin Anko
    implementation "org.jetbrains.anko:anko-commons:$anko_version"
    implementation "org.jetbrains.anko:anko-sdk25:$anko_version" // sdk15, sdk19, sdk21, sdk23 are also available
    implementation "org.jetbrains.anko:anko-sdk25-coroutines:$anko_version"
    // Kotlin DataBinding
    kapt "com.android.databinding:compiler:2.3.3"
    /* For architecture */
    // Dagger 2
    kapt "com.google.dagger:dagger-compiler:2.11"
    kapt "com.google.dagger:dagger-android-processor:2.11"
    implementation "com.google.dagger:dagger:2.11"
    implementation "com.google.dagger:dagger-android-support:2.11"
    /* For reaction programming */
    // RxKotlin
    implementation "io.reactivex.rxjava2:rxkotlin:2.1.0"
    // RxBus
    implementation "com.hwangjr.rxbus:rxbus:2.0.0"
    // RxLifecycle
    implementation "com.trello.rxlifecycle2:rxlifecycle-components:2.1.0"
    implementation "com.trello.rxlifecycle2:rxlifecycle-kotlin:2.1.0"
    // RxBinding
    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:2.0.0"
    implementation "com.jakewharton.rxbinding2:rxbinding-support-v4-kotlin:2.0.0"
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7-kotlin:2.0.0"
    implementation "com.jakewharton.rxbinding2:rxbinding-design-kotlin:2.0.0"
    implementation "com.jakewharton.rxbinding2:rxbinding-recyclerview-v7-kotlin:2.0.0"
    implementation "com.jakewharton.rxbinding2:rxbinding-leanback-v17-kotlin:2.0.0"
    /* For Database */
    kapt "com.github.Raizlabs.DBFlow:dbflow-processor:${dbflow_version}"
    implementation "com.github.Raizlabs.DBFlow:dbflow-core:${dbflow_version}"
    implementation "com.github.Raizlabs.DBFlow:dbflow:${dbflow_version}"
    implementation "com.github.Raizlabs.DBFlow:dbflow-kotlinextensions:${dbflow_version}"
    implementation "com.github.Raizlabs.DBFlow:dbflow-rx2:${dbflow_version}"
    implementation "com.github.Raizlabs.DBFlow:dbflow-rx2-kotlinextensions:${dbflow_version}"
    // sql-cipher database encryption (optional)
//    implementation "com.github.Raizlabs.DBFlow:dbflow-sqlcipher:${dbflow_version}"
//    implementation "net.zetetic:android-database-sqlcipher:${sqlcipher_version}@aar"
    /* For internet */
    // Retrofit 2
    implementation "com.squareup.retrofit2:retrofit:2.3.0"
    implementation "com.squareup.retrofit2:converter-gson:2.3.0"
    implementation "com.squareup.retrofit2:adapter-rxjava2:2.3.0"
    // Glide
    implementation "com.github.bumptech.glide:glide:4.0.0"
    // LastFM api
    implementation "de.u-mass:lastfm-java:0.1.2"
    /* UI */
    // Blur View
    implementation "com.github.mmin18:realtimeblurview:1.1.0"
    implementation "com.mikhaellopez:circularimageview:3.0.2"

    //noinspection GradleCompatible
    implementation "com.android.support:appcompat-v7:25.4.0"
    implementation "com.android.support:cardview-v7:25.4.0"
    implementation "com.android.support:recyclerview-v7:25.4.0"
    implementation "com.android.support.constraint:constraint-layout:1.1.0-beta1"

    // Database debug
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.1'

    testImplementation "junit:junit:4.12"

    androidTestImplementation("com.android.support.test.espresso:espresso-core:2.2.2", {
        exclude group: "com.android.support", module: "support-annotations"
    })
}

repositories {
    mavenCentral()
}

kotlin {
    experimental {
        coroutines "enable"
    }
}
