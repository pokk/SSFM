apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'
apply plugin: 'org.fabiomsr.drawableoptimizer'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def buildDateTime = project.hasProperty('devBuild') ? 100 : new Date().format('yyMMddHHmm').toInteger()

// From Kotlin 1.1.4, we don't have to use findByView in viewHolder or customize view.
androidExtensions { experimental = true }
kapt { generateStubs = true }

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'
    defaultConfig {
        applicationId "taiwan.no1.app"
        minSdkVersion 21
        targetSdkVersion 26
        multiDexEnabled true
        versionCode buildDateTime
        versionName "1.0"
        renderscriptTargetApi 21
        renderscriptSupportModeEnabled true    // Enable RS support
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField "long", "BUILD_TIMESTAMP", project.hasProperty("devBuild") ? "000000000" : System.currentTimeMillis() + "L"
    }
    buildTypes {
        release { minifyEnabled false }
        debug {
            signingConfig signingConfigs.debug
            ext.alwaysUpdateBuildId = false
            resValue("string", "PORT_NUMBER", "8081")
        }
    }
    sourceSets {
        main {
            java.srcDirs += 'src/main/kotlin'
            assets.srcDirs += 'assets'
        }
    }
    packagingOptions { exclude 'META-INF/rxjava.properties' }
    configurations.all { resolutionStrategy.force "com.google.code.findbugs:jsr305:3.0.1" }
    dataBinding { enabled = true }
    dexOptions { javaMaxHeapSize "4g" }
    drawableOptimizer {
        optimizer = 'zopfli'
        compressionLevel = 9
        iterations = 15
        onlyOnRelease = true
        logLevel = 'info'
    }
    if (project.hasProperty('devBuild')) {
        aaptOptions.cruncherEnabled = false
        splits.abi.enable = false
        splits.density.enable = false
    }
    flavorDimensions "default"
    productFlavors {
        development {
            minSdkVersion 21
            dimension "default"
            resConfigs("en", "xxhdpi")
        }
        production { dimension "default" }
    }
    // This is for JreBel simulator. If we just use build-in run, this could be deleted.
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'com.android.support:multidex:1.0.2'

    // My own tool library
    implementation 'com.devrapid.jieyi:adaptiverecyclerview:0.0.6'
    implementation 'com.devrapid.jieyi:musicdiskplayer:0.1.3'
    // Library is including Gson, RxKotlin, RxJava.
    implementation 'com.devrapid.jieyi:kotlinknifer:1.0.13'

    /* For language */
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    //    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    // Kotlin Anko
    implementation "org.jetbrains.anko:anko-commons:$anko_version"
    implementation "org.jetbrains.anko:anko-sdk25:$anko_version"
    // sdk15, sdk19, sdk21, sdk23 are also available
    implementation "org.jetbrains.anko:anko-sdk25-coroutines:$anko_version"
    // Kotlin DataBinding
    kapt "com.android.databinding:compiler:2.3.3"

    /* For architecture */
    // Dagger 2
    kapt "com.google.dagger:dagger-compiler:$dagger2_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger2_version"
    implementation "com.google.dagger:dagger:$dagger2_version"
    implementation "com.google.dagger:dagger-android-support:$dagger2_version"

    /* For reaction programming */
    // RxBus
    implementation('com.hwangjr.rxbus:rxbus:2.0.0', {
        exclude group: 'com.jakewharton.timber', module: 'timber'
    })
    // RxLifecycle
    implementation 'com.trello.rxlifecycle2:rxlifecycle-components:2.1.0'
    implementation 'com.trello.rxlifecycle2:rxlifecycle-kotlin:2.1.0'
    // RxBinding
    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:$rxbinding_vserion"
    implementation "com.jakewharton.rxbinding2:rxbinding-support-v4-kotlin:$rxbinding_vserion"
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7-kotlin:$rxbinding_vserion"
    implementation "com.jakewharton.rxbinding2:rxbinding-design-kotlin:$rxbinding_vserion"
    implementation "com.jakewharton.rxbinding2:rxbinding-recyclerview-v7-kotlin:$rxbinding_vserion"
    implementation "com.jakewharton.rxbinding2:rxbinding-leanback-v17-kotlin:$rxbinding_vserion"

    /* For Database */
    kapt "com.github.Raizlabs.DBFlow:dbflow-processor:$dbflow_version"
    implementation "com.github.Raizlabs.DBFlow:dbflow-core:$dbflow_version"
    implementation "com.github.Raizlabs.DBFlow:dbflow:$dbflow_version"
    implementation "com.github.Raizlabs.DBFlow:dbflow-kotlinextensions:$dbflow_version"
    implementation "com.github.Raizlabs.DBFlow:dbflow-rx2:$dbflow_version"
    implementation "com.github.Raizlabs.DBFlow:dbflow-rx2-kotlinextensions:$dbflow_version"

    // sql-cipher database encryption (optional)
//    implementation "com.github.Raizlabs.DBFlow:dbflow-sqlcipher:$dbflow_version"
//    implementation "net.zetetic:android-database-sqlcipher:${sqlcipher_version}@aar"

    /* Media */
    // ExoPlayer
    implementation "com.google.android.exoplayer:exoplayer:$exo_player"

    /* For internet */
    // Retrofit 2
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:3.9.0"
    // Jsoup
//    implementation 'org.jsoup:jsoup:1.10.3'
    // Glide
    implementation "com.github.bumptech.glide:glide:4.0.0"
    // LastFM api
    implementation "de.u-mass:lastfm-java:0.1.2"

    /* UI */
    // Blur View
    implementation "com.github.mmin18:realtimeblurview:1.1.0"
    implementation "com.mikhaellopez:circularimageview:3.0.2"
    implementation "com.github.Yalantis:GuillotineMenu-Android:1.2"
    implementation "de.hdodenhof:circleimageview:2.2.0"
    //noinspection GradleCompatible
    implementation "com.android.support:appcompat-v7:$android_support_version"
    implementation "com.android.support:palette-v7:$android_support_version"
    implementation "com.android.support:cardview-v7:$android_support_version"
    implementation "com.android.support:recyclerview-v7:$android_support_version"
    implementation "com.android.support.constraint:constraint-layout:1.1.0-beta1"
    // Database debug
    debugImplementation "com.amitshekhar.android:debug-db:1.0.1"

    testImplementation 'junit:junit:4.12'

    androidTestImplementation("com.android.support.test.espresso:espresso-core:2.2.2", {
        exclude group: "com.android.support", module: "support-annotations"
    })
}

repositories {
    mavenCentral()
}

kotlin {
    experimental {
        coroutines "enable"
    }
}
